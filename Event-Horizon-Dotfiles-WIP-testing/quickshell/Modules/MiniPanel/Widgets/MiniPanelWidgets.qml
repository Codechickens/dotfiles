import QtQuick
import QtQuick.Controls
import Quickshell
import Quickshell.Services.Mpris
import Quickshell.Services.Notifications
import Quickshell.Services.SystemTray
import Quickshell.Widgets
import qs.Common
import qs.Modules
import qs.Modules.MiniPanel.Widgets
import qs.Services
import qs.Widgets

Row {
    id: root

    property var widgetList: []
    property string side: "left"
    property var contextMenu
    property real widgetHeight: SettingsData.minipanelHeight
    property real unifiedScaleFactor: SettingsData.minipanelScale
    property real unifiedIconSize: SettingsData.minipanelIconSize
    property real unifiedFontSize: 11
    property real unifiedIconSpacing: SettingsData.minipanelIconSpacing
    property real unifiedPadding: SettingsData.minipanelPadding
    property var parentScreen: null
    property var parentWindow: null
    property var controlCenterLoader: null
    property var appDrawerLoader: null
    property bool isAtBottom: false
    property bool isVertical: false
    property bool isBarVertical: false
    property bool allowEdgeAnchors: false
    property var axis: null

    property var widgetMap: {
        "launcherButton": launcherButtonComponent,
        "workspaceSwitcher": workspaceSwitcherComponent,
        "focusedWindow": focusedWindowComponent,
        "music": mediaComponent,
        "clock": clockComponent,
        "weather": weatherComponent,
        "systemTray": systemTrayComponent,
        "cpuUsage": cpuUsageComponent,
        "memUsage": ramUsageComponent,
        "notificationButton": notificationButtonComponent,
        "battery": batteryComponent,
        "controlCenterButton": controlCenterButtonComponent,
        "systemUpdate": systemUpdateComponent,
        "volumeMixer": volumeMixerComponent,
        "vpn": vpnComponent,
        "gpuTemp": gpuTempComponent,
        "cpuTemp": cpuTempComponent,
        "networkSpeed": networkMonitorComponent,
        "mediaDisplay": mediaDisplayComponent,
        "applications": applicationsComponent,
        "colorPicker": colorPickerComponent,
        "idleInhibitor": idleInhibitorComponent,
        "keyboardLayout": keyboardLayoutComponent,
        "notepadButton": notepadButtonComponent,
        "privacyIndicator": privacyIndicatorComponent,
        "runningApps": runningAppsComponent,
        "darkDash": darkDashComponent,
        "audioVisualization": audioVisualizationComponent
    }

    spacing: SettingsData.minipanelSpacing

    readonly property bool isDock: false

    function getWidget(widgetId) {
        return widgetMap[widgetId] || null
    }

    Component { id: launcherButtonComponent; MiniPanelLauncherButton { } }
    Component { id: workspaceSwitcherComponent; MiniPanelWorkspaceSwitcher { } }
    Component { id: focusedWindowComponent; MiniPanelFocusedApp { } }
    Component { id: mediaComponent; MiniPanelMedia { } }
    Component { id: clockComponent; MiniPanelClock { } }
    Component { id: weatherComponent; MiniPanelWeather { } }
    Component { id: systemTrayComponent; MiniPanelSystemTrayBar { } }
    Component { id: cpuUsageComponent; MiniPanelCpuMonitor { } }
    Component { id: ramUsageComponent; MiniPanelRamMonitor { } }
    Component { id: notificationButtonComponent; MiniPanelNotificationCenterButton { } }
    Component { id: batteryComponent; MiniPanelBattery { } }
    Component { id: controlCenterButtonComponent; MiniPanelControlCenterButton { } }
    Component { id: systemUpdateComponent; MiniPanelSystemUpdate { } }
    Component { id: volumeMixerComponent; MiniPanelVolumeMixerButton { } }
    Component { id: vpnComponent; MiniPanelVpn { } }
    Component { id: gpuTempComponent; MiniPanelGpuTemperature { } }
    Component { id: cpuTempComponent; MiniPanelCpuTemperature { } }
    Component { id: networkMonitorComponent; MiniPanelNetworkMonitor { } }
    Component { id: mediaDisplayComponent; MiniPanelMediaDisplay { } }
    Component { id: applicationsComponent; MiniPanelApplications { } }
    Component { id: colorPickerComponent; MiniPanelColorPicker { } }
    Component { id: idleInhibitorComponent; MiniPanelIdleInhibitor { } }
    Component { id: keyboardLayoutComponent; MiniPanelKeyboardLayoutName { } }
    Component { id: notepadButtonComponent; MiniPanelNotepadButton { } }
    Component { id: privacyIndicatorComponent; MiniPanelPrivacyIndicator { } }
    Component { id: runningAppsComponent; MiniPanelRunningApps { } }
    Component { id: darkDashComponent; MiniPanelDarkDash { } }
    Component { id: audioVisualizationComponent; MiniPanelAudioVisualization { } }

    Repeater {
        model: root.widgetList

        delegate: Item {
            width: loader.width
            height: root.widgetHeight
            visible: loader.status === Loader.Ready

            Loader {
                id: loader
                anchors.verticalCenter: parent.verticalCenter
                anchors.horizontalCenter: parent.horizontalCenter
                sourceComponent: {
                    var id = model.widgetId || modelData
                    return root.getWidget(id)
                }

                property var widgetData: model && model.widgetId ? ({
                    "widgetId": model.widgetId,
                    "enabled": model.enabled,
                    "size": model.size,
                    "selectedGpuIndex": model.selectedGpuIndex,
                    "pciId": model.pciId
                }) : null
                property var modelIndex: index

                onLoaded: {
                    if (item) {
                        if ("widgetData" in item && widgetData) item.widgetData = widgetData
                        if ("modelIndex" in item) item.modelIndex = modelIndex
                        if ("contextMenu" in item) item.contextMenu = root.contextMenu
                        if ("section" in item) item.section = root.side
                        
                        if ("parentScreen" in item) item.parentScreen = root.parentScreen
                    }
                }
            }
        }
    }
}
